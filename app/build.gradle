plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'io.gitlab.arturbosch.detekt' version '1.19.0-RC1'
}

def detektVersion = '1.19.0-RC1'
def composeVersion = '1.0.5'

def properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdk 31

    defaultConfig {
        applicationId "dev.gumil.games"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'CLIENT_ID', "\"${properties.getProperty("clientId")}\""
        buildConfigField 'String', 'TOKEN', "\"${properties.getProperty("token")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def retrofit = '2.9.0'
    def okhttp = '4.9.2'
    def appcompat = '1.4.0'
    def moshi = '1.12.0'

    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.moshi:moshi:$moshi"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"

    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.activity:activity-compose:$appcompat"
    implementation 'com.google.android.material:material:1.4.0'

    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"

    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
}

detekt {
    config = files("$rootDir/config/detekt/detekt.yml")
}
